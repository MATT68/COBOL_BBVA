      * ---------------------------------------------------
      *    AUTHOR    :
      * DESCRIPCION  :
      *
      * ---------------------------------------------------
       IDENTIFICATION DIVISION.
       PROGRAM-ID. PRG110CB.
      ************************************************************
      *  ENVIRONMENT   D I V I S I O N
      ************************************************************
       ENVIRONMENT DIVISION.
      *
       CONFIGURATION SECTION.
       SOURCE-COMPUTER. Z.
      *
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.
      *
           SELECT EMPLE
              ASSIGN EMPLEADO
              ORGANIZATION IS SEQUENTIAL
              ACCESS MODE IS  SEQUENTIAL
              FILE STATUS IS WS-FILE-STATUS.
      *
           SELECT EMPLEBK2
              ASSIGN EMPLEBK2
              ORGANIZATION IS SEQUENTIAL
              ACCESS MODE IS  SEQUENTIAL
              FILE STATUS IS WS-FILE-STATUS.
      *
           SELECT DUPLIK2
              ASSIGN DUPLIK2
              ORGANIZATION IS SEQUENTIAL
              ACCESS MODE IS  SEQUENTIAL
              FILE STATUS IS WS-FILE-STATUS.
      *
           SELECT EMPLES1
              ASSIGN EMPLES1
              ORGANIZATION IS SEQUENTIAL
              ACCESS MODE IS  SEQUENTIAL
              FILE STATUS IS WS-FILE-STATUS.
      *
           SELECT EMPLES2
              ASSIGN EMPLES2
              ORGANIZATION IS SEQUENTIAL
              ACCESS MODE IS  SEQUENTIAL
              FILE STATUS IS WS-FILE-STATUS.
      *
      ************************************************************
      *    D A T A     D I V I S I O N
      ************************************************************
       DATA DIVISION.
       FILE SECTION.
      *    FICHEROS QUE USA EL PROGRAMA
       FD  EMPLE
           RECORDING MODE IS F.
       01  REC-EMPLE                                  PIC X(59).
      *
       FD  EMPLEBK2
           RECORDING MODE IS F.
       01  REC-EMPLEBK2                               PIC X(80).
      *
       FD  DUPLIK2
           RECORDING MODE IS F.
       01  REC-DUPLIK2                                PIC X(90).
      *
       FD  EMPLES1
           RECORDING MODE IS F.
       01  REC-EMPLES1                                PIC X(59).
      *
       FD  EMPLES2
           RECORDING MODE IS F.
       01  REC-EMPLES2                                PIC X(80).
      *
       WORKING-STORAGE SECTION.
      ************************************************************
      *    WORKING STORAGE
      ************************************************************
       01  WS-VARIABLES.
           05  WS-FILE-STATUS                 PIC X(2).
           05  WS-OPERACION                   PIC X(8).
           05  WS-PARRAFO                     PIC X(8).
           05  WS-FICHERO                     PIC X(8).
           05  WS-OTRA                        PIC X(2).
           05  WS-CLIENTE.
               10  WS-APELLIDO2               PIC X(20).
           05  WS-M                           PIC X(8)    VALUE 'M'.
           05  WS-R                           PIC X(8)    VALUE 'R'.
           05  WS-RESULTADO                   PIC 9(10).
      *
           05  WS-NUMEROS.
               10  WS-NUMERO-CALLE            PIC  9(5).
               10  WS-COCIENTE                PIC  9(5) COMP-3.
               10  WS-RESTO                   PIC  9(5) COMP-3.
      *
           05 WS-FECHA1.
               10 WS-DAY1                     PIC X(2).
               10 WS-MONTH1                   PIC X(2).
               10 WS-YEAR1                    PIC X(4).
      *
           05 WS-FECHA2.
               10 WS-YEAR2                    PIC X(4).
               10 WS-MONTH2                   PIC X(2).
               10 WS-DAY2                     PIC X(2).
      *
       01  WC-CONTADORES.
           05  WC-EMPLE                       PIC  9(5).
           05  WC-EMPLEBK2                    PIC  9(5).
           05  WC-DUPLIK2                     PIC  9(5).
           05  WC-EMPLES1                     PIC  9(5).
           05  WC-EMPLES2                     PIC  9(5).
      ************************************************************
      *     LITERALES
      ************************************************************
       01  LT-LITERALES.
           05  LT-SI                          PIC X    VALUE 'S'.
           05  LT-NO                          PIC X    VALUE 'N'.
           05  LT-OK                          PIC XX   VALUE '00'.
           05  LT-FIN-FICHERO                 PIC XX   VALUE '10'.
           05  LT-PROGRAMA                    PIC X(8) VALUE 'PRG110CB'.
           05  LT-RUT-FECHAS                  PIC X(8) VALUE 'RUTFECHA'.
           05  LT-RUT-CODIF                   PIC X(8) VALUE 'RUTCODIF'.
           05  LT-EMPLE                       PIC X(8) VALUE 'EMPLE   '.
           05  LT-EMPLEBK2                    PIC X(8) VALUE 'EMPLEBK2'.
           05  LT-DUPLIK2                     PIC X(8) VALUE 'DUPLIK2 '.
           05  LT-EMPLES1                     PIC X(8) VALUE 'EMPLES1 '.
           05  LT-EMPLES2                     PIC X(8) VALUE 'EMPLES2 '.
           05  LT-OPEN                        PIC X(8) VALUE 'ABRIR   '.
           05  LT-CLOSE                       PIC X(8) VALUE 'CERRAR  '.
           05  LT-READ                        PIC X(8) VALUE 'LECTURA '.
           05  LT-WRITE                       PIC X(8) VALUE 'ESCRIBIR'.
           05  CT-UNO                         PIC 9    VALUE 1.
      *
      ******************************************************************00440422
      **              SWITCHES                                        **00440522
      ******************************************************************00440622
       01  SW-SWITCHES.
           05  SW-FIN-FICHERO                 PIC X    VALUE '2'.
               88  FIN-FICHERO                         VALUE '1'.
               88  NO-FIN-FICHERO                      VALUE '2'.
      *
           05  SW-FIN-FICHERO-BK2             PIC X    VALUE '2'.
               88  FIN-FICHERO-BK2                     VALUE '1'.
               88  NO-FIN-FICHERO-BK2                  VALUE '2'.
      *
           05  SW-ERROR                       PIC X    VALUE '2'.
               88  HAY-ERROR                           VALUE '1'.
               88  NO-HAY-ERROR                        VALUE '2'.
      *
      ******************************************************************00441222
      **              CONSTANTES                                      **00441322
      ******************************************************************00441422
                                                                        00441122
      ******************************************************************00441922
      **        F I C H E R O S                                       **00442022
      ******************************************************************00442122
       COPY COPYEMPL REPLACING ==:XX:== BY ==WS==.
       COPY CPEMPBK2 REPLACING ==:XX:== BY ==WS-EB2==.
       COPY CPDUPLK2.
       COPY CPEMPLE1.
       COPY CPEMPLE2.
      *                                                                 00442122
      ******************************************************************00441922
      **    COPYS DE RUTINAS                                          **00442022
      ******************************************************************00442122
       COPY COPYRUTI.
       COPY COPYRUT2.
      *                                                                 00442122
      ************************************************************
      *      P R O C E D U R E          D I V I S I O N
      ************************************************************
       PROCEDURE DIVISION.
      *
           PERFORM 1000-INICIO
              THRU 1000-INICIO-EXIT.
      *
           PERFORM 2000-PROCESO
              THRU 2000-PROCESO-EXIT
             UNTIL FIN-FICHERO AND
                   FIN-FICHERO-BK2.
      *
           PERFORM 8000-FIN
              THRU 8000-FIN-EXIT.
      *
       1000-INICIO.
      ************************************************************
      *    INICIALIZA VARIABLES
      *    ABRE FICHERO, LEER PRIMER REGISTRO,..
      ************************************************************
           DISPLAY '*******  I N I C I O  ******************'
           INITIALIZE WS-VARIABLES
                      WC-CONTADORES
      *
           SET NO-FIN-FICHERO          TO TRUE.
           SET NO-FIN-FICHERO-BK2      TO TRUE.
           SET NO-HAY-ERROR            TO TRUE.
      *************************************************************
      *     ABRIMOS TODOS LOS FICHEROS
      ************************************************************
           OPEN INPUT EMPLE
      *
           IF  WS-FILE-STATUS = LT-OK
               CONTINUE
           ELSE
               MOVE LT-OPEN  TO WS-OPERACION
               MOVE LT-EMPLE TO WS-FICHERO
               MOVE '1000-'  TO WS-PARRAFO
      *
               PERFORM 9100-ERROR-FICHERO
                  THRU 9100-ERROR-FICHERO-EXIT
           END-IF.
      *
           OPEN INPUT EMPLEBK2
      *
           IF  WS-FILE-STATUS = LT-OK
               CONTINUE
           ELSE
               MOVE LT-OPEN     TO WS-OPERACION
               MOVE LT-EMPLEBK2 TO WS-FICHERO
               MOVE '1000-'     TO WS-PARRAFO
      *
               PERFORM 9100-ERROR-FICHERO
                  THRU 9100-ERROR-FICHERO-EXIT
           END-IF.
      *
           OPEN OUTPUT DUPLIK2
      *
           IF  WS-FILE-STATUS = LT-OK
               CONTINUE
           ELSE
               MOVE LT-OPEN    TO WS-OPERACION
               MOVE LT-DUPLIK2 TO WS-FICHERO
               MOVE '1000-'    TO WS-PARRAFO
      *
               PERFORM 9100-ERROR-FICHERO
                  THRU 9100-ERROR-FICHERO-EXIT
           END-IF.
      *
           OPEN OUTPUT EMPLES1
      *
           IF  WS-FILE-STATUS = LT-OK
               CONTINUE
           ELSE
               MOVE LT-OPEN    TO WS-OPERACION
               MOVE LT-EMPLES1 TO WS-FICHERO
               MOVE '1000-'    TO WS-PARRAFO
      *
               PERFORM 9100-ERROR-FICHERO
                  THRU 9100-ERROR-FICHERO-EXIT
           END-IF.
      *
           OPEN OUTPUT EMPLES2
      *
           IF  WS-FILE-STATUS = LT-OK
               CONTINUE
           ELSE
               MOVE LT-OPEN    TO WS-OPERACION
               MOVE LT-EMPLES2 TO WS-FICHERO
               MOVE '1000-'    TO WS-PARRAFO
      *
               PERFORM 9100-ERROR-FICHERO
                  THRU 9100-ERROR-FICHERO-EXIT
           END-IF.
      *
           PERFORM 9200-LEER-EMPLE
              THRU 9200-LEER-EMPLE-EXIT
      *
           IF  WS-FILE-STATUS = LT-FIN-FICHERO
                   DISPLAY '***********************************'
                   DISPLAY ' FICHERO : ' LT-EMPLE
                   DISPLAY ' FICHERO VACIO  '
                   DISPLAY ' FILE STATUS :   ' WS-FILE-STATUS
                   DISPLAY '***********************************'
           END-IF.
      *
           PERFORM 9300-LEER-EMPLEBK2
              THRU 9300-LEER-EMPLEBK2-EXIT
      *
           IF  WS-FILE-STATUS = LT-FIN-FICHERO
                   DISPLAY '***********************************'
                   DISPLAY ' FICHERO : ' LT-EMPLEBK2
                   DISPLAY ' FICHERO VACIO  '
                   DISPLAY ' FILE STATUS :   ' WS-FILE-STATUS
                   DISPLAY '***********************************'
           END-IF.
      *
       1000-INICIO-EXIT.
           EXIT.
      *
       2000-PROCESO.
      *****************************************************
      *   PROCESAMOS LA FILA LEIDA
      *****************************************************
           MOVE '2000-'        TO WS-PARRAFO.
      *
           IF  WS-NUM-EMPLE = WS-EB2-COD-EMPLEADO
      *        TRATAMOS REGISTROS IGUALES
               PERFORM 2100-ESCRIBE-DUPLIK2
                  THRU 2100-ESCRIBE-DUPLIK2-EXIT
      *
               PERFORM 9200-LEER-EMPLE
                  THRU 9200-LEER-EMPLE-EXIT
      *
               PERFORM 9300-LEER-EMPLEBK2
                  THRU 9300-LEER-EMPLEBK2-EXIT
      *
           ELSE
               IF  WS-NUM-EMPLE > WS-EB2-COD-EMPLEADO
                   PERFORM 2200-ESCRIBE-EMPLES2
                      THRU 2200-ESCRIBE-EMPLES2-EXIT
      *
                   PERFORM 9300-LEER-EMPLEBK2
                      THRU 9300-LEER-EMPLEBK2-EXIT
               ELSE
                   PERFORM 2300-ESCRIBE-EMPLES1
                      THRU 2300-ESCRIBE-EMPLES1-EXIT
      *
                   PERFORM 9200-LEER-EMPLE
                      THRU 9200-LEER-EMPLE-EXIT
               END-IF
           END-IF.
      *
       2000-PROCESO-EXIT.
           EXIT.
      *
       2100-ESCRIBE-DUPLIK2.
      ********************************************************
      *    ESCRIBIMOS EL FICHERO DUPLIK2
      ********************************************************
      *
           INITIALIZE  WS-DP-EMPLEADOS
      *
           MOVE WS-NUM-EMPLE    TO WS-DP-NUM-EMPLE
           MOVE WS-NOMBRE       TO WS-DP-NOMBRE1
      *    MOVE WS-INICIAL      TO
           MOVE WS-APELLIDO     TO WS-DP-APELLIDO1
           MOVE WS-CO-DEPT      TO WS-DP-CO-DEPT1
           MOVE WS-IM-SALARIO   TO WS-DP-IM-SALARIO1
           MOVE WS-IM-COMISION  TO WS-DP-IM-COMISION1
      *
           MOVE WS-EB2-APELLIDO1  TO WS-DP-APELLIDO2
      *    MOVE WS-EB2-APELLIDO2  TO
           MOVE WS-EB2-NOMBRE     TO WS-DP-NOMBRE2
           MOVE WS-EB2-CO-DEPT    TO WS-DP-CO-DEPT2
      *    MOVE WS-EB2-DS-DEPT    TO
      *    MOVE WS-EB2-FX-ALTA    TO
           MOVE WS-EB2-IM-SALARIO TO WS-DP-IM-SALARIO2
           MOVE ZEROS             TO WS-DP-IM-COMISION2
      *
           WRITE REC-DUPLIK2
            FROM WS-DP-EMPLEADOS
      *
           IF  WS-FILE-STATUS = LT-OK
               ADD 1           TO    WC-DUPLIK2
           ELSE
               MOVE LT-WRITE   TO WS-OPERACION
               MOVE LT-DUPLIK2 TO WS-FICHERO
               MOVE '2100-'    TO WS-PARRAFO
      *
               PERFORM 9100-ERROR-FICHERO
                  THRU 9100-ERROR-FICHERO-EXIT
           END-IF.
      *
       2100-ESCRIBE-DUPLIK2-EXIT.
           EXIT.
      *
       2200-ESCRIBE-EMPLES2.
      ********************************************************
      *    ESCRIBIMOS EL FICHERO EMPLEB2
      ********************************************************
      *
           INITIALIZE WS-S2-EMPLE-BANK2
      *
           MOVE WS-EB2-COD-EMPLEADO TO WS-S2-COD-EMPLEADO
           MOVE WS-EB2-APELLIDO1    TO WS-S2-APELLIDO1
           MOVE WS-EB2-APELLIDO2    TO WS-S2-APELLIDO2
           MOVE WS-EB2-NOMBRE       TO WS-S2-NOMBRE
           MOVE WS-EB2-CO-DEPT      TO WS-S2-CO-DEPT
           MOVE WS-EB2-DS-DEPT      TO WS-S2-DS-DEPT
      *
           PERFORM 2210-RUTINA-FECHAS
              THRU 2210-RUTINA-FECHAS-EXIT
      *
           MOVE WS-EB2-IM-SALARIO   TO WS-S2-IM-SALARIO
           MOVE WS-EB2-OFICINA      TO WS-S2-OFICINA
      *
           WRITE REC-EMPLES2
            FROM WS-S2-EMPLE-BANK2
      *
           IF  WS-FILE-STATUS = LT-OK
               ADD 1           TO    WC-EMPLES2
           ELSE
               MOVE LT-WRITE   TO WS-OPERACION
               MOVE LT-EMPLES2 TO WS-FICHERO
               MOVE '2200-'    TO WS-PARRAFO
      *
               PERFORM 9100-ERROR-FICHERO
                  THRU 9100-ERROR-FICHERO-EXIT
           END-IF.
      *
       2200-ESCRIBE-EMPLES2-EXIT.
           EXIT.
      *
       2210-RUTINA-FECHAS.
      ********************************************************
      *   LLAMAMOS A A LA RUTINA DE FECHAS PARA CAMBIO FMT
      ********************************************************
           MOVE '01'                TO CP-TIPO-I
           MOVE WS-EB2-FX-ALTA      TO CP-FECHA-I
      *
           CALL LT-RUT-FECHAS USING CP-RT-FECHAS
      *
           IF  CP-COD-RETORNO-O = LT-OK
               MOVE CP-FECHA-O          TO WS-S2-FX-ALTA
           ELSE
               MOVE CP-MSG-ERROR  TO WS-OPERACION
               MOVE LT-RUT-FECHAS TO WS-FICHERO
               MOVE '2210-'       TO WS-PARRAFO
      *
               PERFORM 9100-ERROR-FICHERO
                  THRU 9100-ERROR-FICHERO-EXIT
           END-IF.
       2210-RUTINA-FECHAS-EXIT.
           EXIT.
      *
       2300-ESCRIBE-EMPLES1.
      ********************************************************
      *    ESCRIBIMOS EL FICHERO EMPLE1
      ********************************************************
      *
           INITIALIZE WS-S1-EMPLEADOS
      *
           MOVE WS-NUM-EMPLE    TO WS-S1-NUM-EMPLE
           MOVE WS-NOMBRE       TO WS-S1-NOMBRE
           MOVE WS-INICIAL      TO WS-S1-INICIAL
           MOVE WS-APELLIDO     TO WS-S1-APELLIDO
           MOVE WS-CO-DEPT      TO WS-S1-CO-DEPT
           MOVE WS-IM-SALARIO   TO WS-S1-IM-SALARIO
           MOVE WS-IM-COMISION  TO WS-S1-IM-COMISION
      *
           WRITE REC-EMPLES1
            FROM WS-S1-EMPLEADOS
      *
           IF  WS-FILE-STATUS = LT-OK
               ADD 1           TO    WC-EMPLES1
           ELSE
               MOVE LT-WRITE   TO WS-OPERACION
               MOVE LT-EMPLES1 TO WS-FICHERO
               MOVE '2300-'    TO WS-PARRAFO
      *
               PERFORM 9100-ERROR-FICHERO
                  THRU 9100-ERROR-FICHERO-EXIT
           END-IF.
      *
       2300-ESCRIBE-EMPLES1-EXIT.
           EXIT.
      *
       8000-FIN.
      ********************************************************
      *    CERRAR FICHEROS, ETC.
      ********************************************************
           IF  NO-HAY-ERROR
               DISPLAY '*****************************************'
               DISPLAY '***** ESTADISTICAS DEL PROGRAMA : ' LT-PROGRAMA
               DISPLAY '**                                          **'
               DISPLAY '** TOTAL FILAS LEIDAS EMPLE  : ' WC-EMPLE
               DISPLAY '** TOTAL FILAS LEIDAS EMPLBK2: ' WC-EMPLEBK2
               DISPLAY '** TOTAL FILAS ESCR. EMPLES1 : ' WC-EMPLES1
               DISPLAY '** TOTAL FILAS ESCR. EMPLES2 : ' WC-EMPLES2
               DISPLAY '** TOTAL FILAS ESCR. DUPLIK2 : ' WC-DUPLIK2
               DISPLAY '*********************************************'
      *
               CLOSE EMPLE
      *
               IF  WS-FILE-STATUS = LT-OK
                   CONTINUE
               ELSE
                   MOVE LT-CLOSE   TO WS-OPERACION
                   MOVE LT-EMPLE   TO WS-FICHERO
                   MOVE '8000-'    TO WS-PARRAFO
      *
                   PERFORM 9100-ERROR-FICHERO
                      THRU 9100-ERROR-FICHERO-EXIT
               END-IF
      *
               CLOSE EMPLEBK2
      *
               IF  WS-FILE-STATUS = LT-OK
                   CONTINUE
               ELSE
                   MOVE LT-CLOSE    TO WS-OPERACION
                   MOVE LT-EMPLEBK2 TO WS-FICHERO
                   MOVE '8000-'     TO WS-PARRAFO
      *
                   PERFORM 9100-ERROR-FICHERO
                      THRU 9100-ERROR-FICHERO-EXIT
               END-IF
      *
               CLOSE DUPLIK2
      *
               IF  WS-FILE-STATUS = LT-OK
                   CONTINUE
               ELSE
                   MOVE LT-CLOSE    TO WS-OPERACION
                   MOVE LT-DUPLIK2  TO WS-FICHERO
                   MOVE '8000-'     TO WS-PARRAFO
      *
                   PERFORM 9100-ERROR-FICHERO
                      THRU 9100-ERROR-FICHERO-EXIT
               END-IF
      *
               CLOSE EMPLES1
      *
               IF  WS-FILE-STATUS = LT-OK
                   CONTINUE
               ELSE
                   MOVE LT-CLOSE    TO WS-OPERACION
                   MOVE LT-EMPLES1  TO WS-FICHERO
                   MOVE '8000-'     TO WS-PARRAFO
      *
                   PERFORM 9100-ERROR-FICHERO
                      THRU 9100-ERROR-FICHERO-EXIT
               END-IF
      *
               CLOSE EMPLES2
      *
               IF  WS-FILE-STATUS = LT-OK
                   CONTINUE
               ELSE
                   MOVE LT-CLOSE    TO WS-OPERACION
                   MOVE LT-EMPLES2  TO WS-FICHERO
                   MOVE '8000-'     TO WS-PARRAFO
      *
                   PERFORM 9100-ERROR-FICHERO
                      THRU 9100-ERROR-FICHERO-EXIT
               END-IF
      *
      *
           END-IF.
      *
           STOP RUN.
      *
       8000-FIN-EXIT.
           EXIT.
       9100-ERROR-FICHERO.
      ********************************************************
      *   GESTION DE ERRORES
      ********************************************************
           SET HAY-ERROR TO TRUE
      *
           DISPLAY '**********************************************'
           DISPLAY '****        ERROR EN FICHERO '
           DISPLAY '****   OPERACION   :   ' WS-OPERACION
           DISPLAY '****   FICHERO     :   ' WS-FICHERO
           DISPLAY '****   PARRAFO     :   ' WS-PARRAFO
           DISPLAY '****   FILE STATUS :   ' WS-FILE-STATUS
           DISPLAY '**********************************************'
           .
      *
           PERFORM 8000-FIN
              THRU 8000-FIN-EXIT.
      *
       9100-ERROR-FICHERO-EXIT.
           EXIT.
      *
       9200-LEER-EMPLE.
      ********************************************************
      *   LEEMOS EL FICHERO DE EMPLEADOS
      ********************************************************
           READ EMPLE
              INTO WS-EMPLEADOS
              AT END
                  SET  FIN-FICHERO TO TRUE
                  MOVE HIGH-VALUES TO WS-NUM-EMPLE
           END-READ.
      *
           IF  WS-FILE-STATUS = LT-OK
               ADD 1           TO WC-EMPLE
           ELSE
               IF  WS-FILE-STATUS = LT-FIN-FICHERO
                   CONTINUE
               ELSE
                   MOVE LT-READ    TO WS-OPERACION
                   MOVE LT-EMPLE   TO WS-FICHERO
                   MOVE '9200-'    TO WS-PARRAFO
      *
                   PERFORM 9100-ERROR-FICHERO
                      THRU 9100-ERROR-FICHERO-EXIT
               END-IF
           END-IF.
      *
       9200-LEER-EMPLE-EXIT.
           EXIT.
      *
       9300-LEER-EMPLEBK2.
      ********************************************************
      *   LEEMOS EL FICHERO DE EMPLEADOS DEL BANCO 2
      ********************************************************
           READ EMPLEBK2
              INTO WS-EB2-EMPLE-BANK2
              AT END
                  SET  FIN-FICHERO-BK2 TO TRUE
                  MOVE HIGH-VALUES     TO WS-EB2-COD-EMPLEADO
           END-READ.
      *
           IF  WS-FILE-STATUS = LT-OK
               ADD 1           TO WC-EMPLEBK2
           ELSE
               IF  WS-FILE-STATUS = LT-FIN-FICHERO
                   CONTINUE
               ELSE
                   MOVE LT-READ     TO WS-OPERACION
                   MOVE LT-EMPLEBK2 TO WS-FICHERO
                   MOVE '9300-'     TO WS-PARRAFO
      *
                   PERFORM 9100-ERROR-FICHERO
                      THRU 9100-ERROR-FICHERO-EXIT
               END-IF
           END-IF.
      *
       9300-LEER-EMPLEBK2-EXIT.
           EXIT.
