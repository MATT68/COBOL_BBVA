      **************************************************************
      *
      * DESCRIPCION  : ESTE PROGRAMA LEE VENDEDOR Y GENERA UN
      *             NUEVO FICHERO VENDC3PO
      *
      **************************************************************
       IDENTIFICATION DIVISION.
       PROGRAM-ID. PRG060CB.
      ************************************************************
      *  ENVIRONMENT   D I V I S I O N
      ************************************************************
       ENVIRONMENT DIVISION.
      *
       CONFIGURATION SECTION.
       SOURCE-COMPUTER. Z.
      *
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.
      *
           SELECT VENDE
              ASSIGN VENDEDOR
              ORGANIZATION IS SEQUENTIAL
              ACCESS MODE IS  SEQUENTIAL
              FILE STATUS IS WS-FILE-STATUS.
      *
           SELECT VENDE3
              ASSIGN VENDC3PO
              ORGANIZATION IS SEQUENTIAL
              ACCESS MODE IS  SEQUENTIAL
              FILE STATUS IS WS-FILE-STATUS.
      *
      ************************************************************
      *    D A T A     D I V I S I O N
      ************************************************************
       DATA DIVISION.
       FILE SECTION.
      *    FICHEROS QUE USA EL PROGRAMA
       FD  VENDE
           RECORDING MODE IS F
           RECORD CONTAINS 67 CHARACTERS
           DATA RECORD IS REC-VENDE.
       01  REC-VENDE                           PIC X(67).
      *
       FD  VENDE3
           RECORDING MODE IS F
           RECORD CONTAINS 72 CHARACTERS
           DATA RECORD IS REC-VENDE3.
       01  REC-VENDE3                          PIC X(72).
      *
       WORKING-STORAGE SECTION.
      ************************************************************
      *    WORKING STORAGE
      ************************************************************
       01  WS-VARIABLES.
           05  WS-FILE-STATUS                 PIC X(2).
           05  WS-OPERACION                   PIC X(8).
           05  WS-PARRAFO                     PIC X(8).
           05  WS-FICHERO-ERROR               PIC X(8).
           05  WS-CLIENTE.
               10  WS-APELLIDO2               PIC X(20).
      *
           05  WS-NUMEROS.
               10  WS-NUMERO-CALLE                PIC  9(5).
               10  WS-COCIENTE                    PIC  9(5) COMP-3.
               10  WS-RESTO                       PIC  9(5) COMP-3.
      *
       01  WC-CONTADORES.
           05  WC-CONTADOR                    PIC  9(5).

      ************************************************************
      *     LITERALES
      ************************************************************
       01  LT-LITERALES.
           05  LT-SI                          PIC X    VALUE 'S'.
           05  LT-NO                          PIC X    VALUE 'N'.
           05  LT-LIM-INF                     PIC X    VALUE 'M'.
           05  LT-LIM-SUP                     PIC X    VALUE 'R'.
           05  LT-OK                          PIC XX   VALUE '00'.
           05  LT-FIN-FICHERO                 PIC XX   VALUE '10'.
           05  LT-FICHERO-VENDE               PIC X(8) VALUE 'VENDEDOR'.
           05  LT-FICHERO-VENDE3              PIC X(8) VALUE 'VENDC3PO'.
           05  LT-PROGRAMA                    PIC X(8) VALUE 'PRG060CB'.
           05  LT-OPEN                        PIC X(8) VALUE 'ABRIR   '.
           05  LT-CLOSE                       PIC X(8) VALUE 'CERRAR  '.
           05  LT-READ                        PIC X(8) VALUE 'LECTURA '.
           05  LT-WRITE                       PIC X(8) VALUE 'ESCRIBIR'.
           05  CT-UNO                         PIC 9    VALUE 1.
      *
      ******************************************************************00440422
      **              SWITCHES                                        **00440522
      ******************************************************************00440622
       01  SW-SWITCHES.
           05  SW-FIN-FICHERO                 PIC X    VALUE '2'.
               88  FIN-FICHERO                         VALUE '1'.
               88  NO-FIN-FICHERO                      VALUE '2'.
      *
      ******************************************************************00441222
      **              CONSTANTES                                      **00441322
      ******************************************************************00441422
                                                                        00441122
      ******************************************************************00441922
      **        F I C H E R O S                                       **00442022
      ******************************************************************00442122
       COPY COPYVEND REPLACING ==:XX:== BY ==WS==.
      *
       01  WS-SAL-VENDEDOR.
           05 WS-SAL-COD-VENDEDOR    PIC X(06).
           05 WS-SAL-NOMBRE          PIC X(15).
           05 WS-SAL-APELLIDO        PIC X(20).
           05 WS-SAL-FX-ALTA         PIC X(8).
           05 WS-SAL-IM-SALARIO      PIC 9(6)V99.
           05 WS-SAL-IM-SALARIO-COMP PIC 9(6)V99 COMP-3.
           05 WS-SAL-SECCION         PIC X.
           05 WS-SAL-COD-RESPONSABLE PIC X(6).
      ******************************************************************00442122
      *      P R O C E D U R E          D I V I S I O N
      ************************************************************
       PROCEDURE DIVISION.

           PERFORM 1000-INICIO
              THRU 1000-INICIO-EXIT.
      *
           PERFORM 2000-PROCESO
              THRU 2000-PROCESO-EXIT
             UNTIL FIN-FICHERO.
      *
           PERFORM 8000-FIN
              THRU 8000-FIN-EXIT.
      *
       1000-INICIO.
      ************************************************************
      *    INICIALIZA VARIABLES
      *    ABRE FICHERO, LEER PRIMER REGISTRO,..
      ************************************************************
           DISPLAY '*******  I N I C I O  ******************'
           INITIALIZE WS-VARIABLES
                      WC-CONTADORES
                      WS-VENDEDORES
      *
           SET NO-FIN-FICHERO          TO TRUE.
      *
           OPEN INPUT VENDE
      *
           IF  WS-FILE-STATUS = LT-OK
               CONTINUE
           ELSE
               MOVE LT-FICHERO-VENDE TO WS-FICHERO-ERROR
               MOVE LT-OPEN          TO WS-OPERACION
               MOVE '1000-'          TO WS-PARRAFO
      *
               PERFORM 9100-ERROR-FICHERO
                  THRU 9100-ERROR-FICHERO-EXIT
           END-IF.
      *
           OPEN OUTPUT VENDE3
      *
           IF  WS-FILE-STATUS = LT-OK
               CONTINUE
           ELSE
               MOVE LT-FICHERO-VENDE3 TO WS-FICHERO-ERROR
               MOVE LT-OPEN           TO WS-OPERACION
               MOVE '1000-'           TO WS-PARRAFO
      *
               PERFORM 9100-ERROR-FICHERO
                  THRU 9100-ERROR-FICHERO-EXIT
           END-IF.
      *
           READ VENDE
              INTO WS-VENDEDORES
              AT END
                   SET FIN-FICHERO TO TRUE
           END-READ
      *
           IF  WS-FILE-STATUS = LT-OK
               CONTINUE
           ELSE
               IF  WS-FILE-STATUS = LT-FIN-FICHERO
                   DISPLAY '***********************************'
                   DISPLAY ' FICHERO VACIO  '
                   DISPLAY ' FILE STATUS :   ' WS-FILE-STATUS
                   DISPLAY '***********************************'
               ELSE
                   MOVE LT-READ          TO WS-OPERACION
                   MOVE '1000-'          TO WS-PARRAFO
                   MOVE LT-FICHERO-VENDE TO WS-FICHERO-ERROR
      *
                   PERFORM 9100-ERROR-FICHERO
                      THRU 9100-ERROR-FICHERO-EXIT
               END-IF
           END-IF.
      *
       1000-INICIO-EXIT.
           EXIT.
      *
       2000-PROCESO.
      *****************************************************
      *   PROCESAMOS LA FILA LEIDA
      *****************************************************
           ADD 1              TO WC-CONTADOR.
      *    DISPLAY '** P R O C E S O   ***' WC-CONTADOR
      *
           PERFORM 2100-ESCRIBE-VEND3
              THRU 2100-ESCRIBE-VEND3-EXIT
      *
           READ VENDE
              INTO WS-VENDEDORES
              AT END
                   SET FIN-FICHERO TO TRUE
           END-READ
      *
           IF  WS-FILE-STATUS = LT-OK OR
               WS-FILE-STATUS = LT-FIN-FICHERO
               CONTINUE
           ELSE
               MOVE LT-READ          TO WS-OPERACION
               MOVE '2000-'          TO WS-PARRAFO
               MOVE LT-FICHERO-VENDE TO WS-FICHERO-ERROR
      *
               PERFORM 9100-ERROR-FICHERO
                  THRU 9100-ERROR-FICHERO-EXIT
           END-IF
           .
      *
       2000-PROCESO-EXIT.
           EXIT.
       2100-ESCRIBE-VEND3.
      ********************************************************
      *    ESCRIBIMOS EL NUEVO FICHERO VENDE3
      ********************************************************
           DISPLAY ' WRITING . . . . . . .          '
      *
           MOVE WS-COD-VENDEDOR    TO WS-SAL-COD-VENDEDOR
           MOVE WS-NOMBRE          TO WS-SAL-NOMBRE
           MOVE WS-APELLIDO        TO WS-SAL-APELLIDO
           MOVE WS-FX-ALTA         TO WS-SAL-FX-ALTA
           MOVE WS-IM-SALARIO      TO WS-SAL-IM-SALARIO
           MOVE WS-IM-SALARIO      TO WS-SAL-IM-SALARIO-COMP
           MOVE WS-SECCION         TO WS-SAL-SECCION
           MOVE WS-COD-RESPONSABLE TO
                                   WS-SAL-COD-RESPONSABLE
      *
           WRITE REC-VENDE3
              FROM  WS-SAL-VENDEDOR.
      *
           IF  WS-FILE-STATUS = LT-OK
               CONTINUE
           ELSE
               MOVE LT-WRITE          TO WS-OPERACION
               MOVE '2100-'           TO WS-PARRAFO
               MOVE LT-FICHERO-VENDE3 TO WS-FICHERO-ERROR
      *
               PERFORM 9100-ERROR-FICHERO
                  THRU 9100-ERROR-FICHERO-EXIT
           END-IF
           .
      *
       2100-ESCRIBE-VEND3-EXIT.
           EXIT.
      *
       8000-FIN.
      ********************************************************
      *    CERRAR FICHEROS, ETC.
      ********************************************************
           DISPLAY '**************************************************'
           DISPLAY '***** ESTADISTICAS DEL PROGRAMA : ' LT-PROGRAMA
           DISPLAY '** TOTAL FILAS LEIDAS : ' WC-CONTADOR '        **'
           DISPLAY '**************************************************'
      *
           CLOSE VENDE
      *
           IF  WS-FILE-STATUS = LT-OK
               CONTINUE
           ELSE
               MOVE LT-CLOSE          TO WS-OPERACION
               MOVE '8000-'           TO WS-PARRAFO
               MOVE LT-FICHERO-VENDE  TO WS-FICHERO-ERROR
      *
               PERFORM 9100-ERROR-FICHERO
                  THRU 9100-ERROR-FICHERO-EXIT
           END-IF
      *
           CLOSE VENDE3
      *
           IF  WS-FILE-STATUS = LT-OK
               CONTINUE
           ELSE
               MOVE LT-CLOSE          TO WS-OPERACION
               MOVE '8000-'           TO WS-PARRAFO
               MOVE LT-FICHERO-VENDE3 TO WS-FICHERO-ERROR
      *
               PERFORM 9100-ERROR-FICHERO
                  THRU 9100-ERROR-FICHERO-EXIT
           END-IF
      *
           STOP RUN
           .
      *
       8000-FIN-EXIT.
           EXIT.
      *
       9100-ERROR-FICHERO.
      ********************************************************
      *   GESTION DE ERRORES
      ********************************************************
           DISPLAY '**********************************************'
           DISPLAY '****        ERROR EN FICHERO '
           DISPLAY '****   NOMBRE      :   ' WS-FICHERO-ERROR
           DISPLAY '****   OPERACION   :   ' WS-OPERACION
           DISPLAY '****   PARRAFO     :   ' WS-PARRAFO
           DISPLAY '****   FILE STATUS :   ' WS-FILE-STATUS
           DISPLAY '**********************************************'.
      *
           PERFORM 8000-FIN
              THRU 8000-FIN-EXIT.
      *
       9100-ERROR-FICHERO-EXIT.
           EXIT.

